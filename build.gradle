plugins {
    id 'java'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

sourceCompatibility = '6'
targetCompatibility = '6'

group 'net.covers1624'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.ow2.asm:asm:9.2'
    implementation 'org.ow2.asm:asm-tree:9.2'
}

license {
    header = file("LICENSE-header.txt")
    properties {
        year = new Date().format("yyyy")
    }

    include 'net/creeperhost/**'
    newLine false
}

jar.enabled false
assemble.dependsOn shadowJar
shadowJar {
    configurations = [project.configurations.runtimeClasspath]

    classifier ''
    manifest {
        attributes 'Premain-Class': 'net.creeperhost.log4jpatcher.Main'
    }
    exclude '**/module-info.class'
    relocate 'org/objectweb', 'net/creeperhost/repack/org/objectweb'
}
